<style>
    body {
        margin: min(4vw, 25px);
        font-family: Lato, sans-serif;
    }
    .material-icons { user-select: none; }
    /* Rules for using icons as black on a light background. */
    .material-icons.md-dark { color: rgba(0, 0, 0, 0.54); }
    .material-icons.md-dark.md-inactive { color: rgba(0, 0, 0, 0.26); }

    /* Rules for using icons as white on a dark background. */
    .material-icons.md-light { color: rgba(255, 255, 255, 1); }
    .material-icons.md-light.md-inactive { color: rgba(255, 255, 255, 0.3); }

</style>
<script type="statebus">
    document.title = 'Braid News'
    window.unslash = (t) -> if t?.startsWith("/") then t.substr(1) else t

    dom.BODY = ->
        DIV
            maxWidth: "800px"
            margin: "auto"

            H1 "Braid News"

            USER()
            POSTS()

    dom.POSTS = ->

        # Local copy of posts, ordered.
        posts = (fetch "/posts").all ? []

        ordered = sort_posts posts

        DIV 
            key: "post-list"
            for post in ordered
                RENDER_POST
                    post: post

    # The layout for a single post, including slidergram and such
    dom.RENDER_POST = (post) ->
        # Subscribe to the post
        if post.key then fetch post
        author = fetch post.user

        hover = fetch "hover#{post.key}" 
        hover.hover ?= false

        c = fetch '/current_user'

        clickable = c.logged_in and \
            (c.user.key != author.key) and \
            not (fetch "/votes/_#{unslash c.user.key}_#{unslash author.key}_")?.value?
        DIV
            key: "post-#{post.key}"
            marginTop: "10px"
            marginBottom: "10px"
            display: "flex"
            alignItems: "center"

            SPAN
                width: 30
                height: 30
                lineHeight: "30px"
                textAlign: "center"
                opacity: if clickable and hover.hover then 0.54 else 0
                fontWeight: "bold"
                fontSize: 24
                userSelect: "none"
                "+"

            AVATAR
                user: author
                style:
                    width: 30
                    height: 30
                    borderRadius: "50%"
                    cursor: if clickable then "pointer"

                onClick: if clickable then (e) ->
                    add_user_weight author.key
                    hover.hover = false
                    save hover

                onMouseOver: (e) ->
                    hover.hover = true
                    save hover

                onMouseOut: (e) ->
                    hover.hover = false
                    save hover

            DIV
                display: "inline-flex"
                flexDirection: "row"
                overflowX: "hidden"
                alignItems: "center"
                marginLeft: "10px"
                flexGrow: 1
                cursor: if post.url.length then "pointer"

                onClick: (e) ->
                    if post.url.length
                        window.location.assign post.url

                SPAN
                    marginRight: "10px"
                    fontSize: "1.3em"
                    "#{post.title}"
                SPAN
                    fontSize: "1em"
                    color: "#444"
                    overflowX: "hidden"
                    "#{post.url}"

            DIV
                gridArea: "slider"
                SLIDERGRAM
                    sldr: "/votes_on#{post.key}"
                    width: 200
                    height: 35
                    max_avatar_radius: 15
                    read_only: !c.logged_in


    # Information about the current user:
    # Login/Out/Register
    # Vote on other users
    # Submit a new link
    dom.USER = ->
        c = fetch '/current_user'
        if c.logged_in
            # My Personal Rankings
            s = fetch "show_settings"
            s.show ?= false
            DIV
                width: "100\%"
                # User Settings Bar
                DIV
                    width: "min(400px, 75%)"
                    marginLeft: "auto"
                    marginRight: "auto"
                    display: "flex"
                    flexDirection: "row"
                    alignItems: "center"

                    AVATAR
                        user: c.user
                        style:
                            placeSelf: "center"
                            width: 30
                            height: 30
                            gridArea: "icon"
                            borderRadius: "50%"

                    H3
                        marginLeft: "6px"
                        flexGrow: 1
                        c.user.name

                    SPAN
                        className: "material-icons md-dark"
                        fontSize: "20px"
                        cursor: "pointer"
                        marginRight: "5px"
                        onClick: (e) ->
                            s.show = !s.show ? true
                            save s
                        "settings"
                    SPAN
                        className: "material-icons md-dark"
                        fontSize: "20px"
                        cursor: "pointer"
                        onClick: (e) ->
                            c.logout = true
                            save c
                        "logout"
                # Slidegram and web-of-trust, or account settings
                if s.show then SETTINGS() else NETWORK()
                # Submit Box
                DIV
                    width: "min(600px, 90%)"
                    marginLeft: "auto"
                    marginRight: "auto"
                    display: "flex"
                    flexDirection: "row"
                    justifyContent: "center"
                    DIV
                        fontWeight: "bold"
                        marginRight: "15px"
                        "New Post:"
                    INPUT
                        id: "post-title"
                        placeholder: "Title"
                        flexGrow: "3"
                    INPUT
                        id: "post-url"
                        type: "url"
                        placeholder: "URL"
                        flexGrow: "2"
                        marginLeft: "5px"
                        marginRight: "15px"
                    SPAN
                        className: "material-icons md-dark"
                        onClick: (e) ->
                            title = document.getElementById("post-title")
                            link = document.getElementById("post-url")
                            if title.value.length > 1
                                make_post title.value, link.value, c.user.key
                                title.value = ""
                                link.value = ""
                        cursor: "pointer"
                        "post_add"
        else
            LOGIN
                c: c

    dom.NETWORK = ->
        # // My IDE doesn't know that this isn't javascript
        # // Two things go here:
        # // The users slidergram (allowing you to change the vote you've previously made)
        # // A box to create a new vote
        DIV
            width: "min(700px, 95%)"
            marginLeft: "auto"
            marginRight: "auto"
            padding: "0 15px 15px 15px"
            display: "flex"
            alignItems: "center"
            flexDirection: "column"

            MULTIGRAM
                sldr: 'multigram'
                width: 500
                height: 40
                max_avatar_radius: 15
            # Does this thing need to have suggestions?
            DIV 
                display: "flex"
                alignItems: "center"
                INPUT
                    placeholder: "user/someone"
                    id: "add-follow"
                    height: 16
                SPAN
                    className: "material-icons md-dark"
                    marginLeft: "7px"
                    onClick: (e) ->
                        box = document.getElementById("add-follow")
                        if add_user_weight box.value
                            box.value = ""
                    fontSize: 24
                    cursor: "pointer"
                    "person_add"

    # Attempt to add a neutral vote on the specified user.
    # The return value is whether or not *such a vote exists*.
    # That is, returns true if the desired vote already exists or it was created successfuly.
    # Returns false if the user does not exist or we are not logged in.
    add_user_weight = (username) ->
        t = unslash username
        c = fetch "/current_user"
        unless (t.startsWith "user\/" ) and ((fetch "/#{t}").name?) and (c.logged_in) and (unslash c.user.key != t)
            return false
        # Ok, the input is the username of a real user, and we are also logged in.
        # But have we already voted on them?
        key = "/votes/_#{unslash c.user.key}_#{t}_"
        if (fetch key).value?
            # Also clear the input
            return true
        # Ok, we have not voted on this user yet.
        # Make a default vote.
       
        votes = fetch "/votes_on/#{t}"
        votes.values ?= []
        votes.values.push
            user: c.user.key
            value: 0.5
            key: key
            target: "/#{t}"

        save votes

        return true

    multigram_aux = {}

    bus('multigram').to_fetch = () ->
        c = fetch "/current_user"
        unless c.logged_in
            return {key: 'muligram'}

        votes_arr = Object.values(fetch "/votes_by#{c.user.key}")
            .filter (v) ->
                unless v.target?
                    return false
                (unslash v.target).startsWith "user"
            .map (v) ->
                # Subscribe to each individual vote...
                fetch v
                # Now make a copy of it that isnt tied to the real state url
                t = Object.assign {}, v
                delete t.key
                t

        # if we want to "suggest" users in the extended network that could be voted on, we should pull them from "weights" and include them here.

        Object.assign multigram_aux, {values: votes_arr}

    bus('multigram').to_save = (val, t) ->

        #// Save the auxilliary data
        Object.assign multigram_aux, val
        #// We don't actually want to save thing like mouse state to the server, but we need to keep a copy of it.

        #// The multigram can't add new values, so we just have to save the values in it to the server.
        #// Ie, the multigram has an array of values, each of which is an actual vote state.
        #// But, we only save these if the user is done dragging!
        unless val.dragging
            val.values.forEach (v) ->
                cop = Object.assign {}, v
                cop.key = "/votes/_#{unslash v.user}_#{unslash v.target}_"
                save cop
        #// Then save it:
        t.done val


</script>
<script src="https://stateb.us/client6.js" server="/"></script>
<script src="/coffee/post.coffee" crossorigin=anonymous></script>
<script src="/coffee/login.coffee" crossorigin=anonymous></script>

<!-- For slidergrams -->

<script src="https://considerit.us:3006/client/shared.coffee"></script>
<script src="/coffee/avatar.coffee" crossorigin=anonymous></script>
<script src="/coffee/slidergrams.coffee" crossorigin=anonymous></script>
<script src="/coffee/multigrams.coffee" crossorigin=anonymous></script>

<script src="https://considerit.us:3006/static/vendor/md5.js"></script>
<script src="https://considerit.us:3006/static/vendor/d3.quadtree.js"></script>

<!-- Google Fonts: Lato -->
<link rel="preconnect" href="https://fonts.googleapis.com">
<link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
<link href="https://fonts.googleapis.com/css2?family=Lato:wght@300;400;700&display=swap" rel="stylesheet">
<link href="https://fonts.googleapis.com/icon?family=Material+Icons" rel="stylesheet">

