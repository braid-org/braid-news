<style>
    body {
        margin: min(4vw, 25px);
    }
</style>
<script type="statebus">
    document.title = 'Braid News Placeholder'
    window.unslash = (t) -> if t?.startsWith("/") then t.substr(1) else t

    dom.BODY = ->
        DIV
            maxWidth: "800px"
            margin: "auto"

            H1 "Braid News"

            USER()
            POSTS()

    dom.POSTS = ->

        # Local copy of posts, ordered.
        posts = (fetch "/posts").all ? []

        ordered = sort_posts posts

        DIV 
            key: "post-list"
            for post in ordered
                RENDER_POST
                    post: post

    # The layout for a single post, including slidergram and such
    dom.RENDER_POST = (post) ->
        # Subscribe to the post
        if post.key then fetch post
        author = fetch post.user
        hashcolor = Array.from(author.key.substr("/user/".length, 3))
            .map (c) -> (c.charCodeAt(0) % 16).toString(16)
            .join ""
        # ugly, find a better way to format a time differential
        time = (Date.now() / 1000) - post.time
        suffix = "seconds"
        if time > 60
            time /= 60
            suffix = "minutes"
            if time > 60
                time /= 60
                suffix = "hours"
                if time > 24
                    time /= 24
                    suffix = "days"
                    if time > 30
                        time /= 30
                        suffix = "months"
        time = Math.round time

        c = fetch '/current_user'
        DIV
            key: "post-#{post.key}"
            marginTop: "10px"
            display: "grid"
            grid: '"icon details slider" 1.5em
                   "title title slider" auto
                  / 1.5em 2fr 1fr'
            DIV
                width: "1.4em"
                height: "1.4em"
                placeSelf: "center"
                borderRadius: "1em"
                gridArea: "icon"
                # or a photo
                background: "##{hashcolor}"
            DIV
                gridArea: "details"
                color: "#444"
                alignSelf: "center"
                marginLeft: "7px"
                "#{author.name},  #{time} #{suffix} ago"
            DIV
                gridArea: "title"
                fontSize: "1.3em"
                marginLeft: "10px"
                "#{post.title}"
            DIV
                gridArea: "slider"
                SLIDERGRAM
                    sldr: "/votes_on#{post.key}"
                    width: 200
                    height: 40
                    max_avatar_radius: 15
                    read_only: !c.logged_in


    # Information about the current user:
    # Login/Out/Register
    # Vote on other users
    # Submit a new link
    dom.USER = ->
        c = fetch '/current_user'
        if c.logged_in
            # My Personal Rankings
            s = fetch "show_settings"
            s.show ?= false
            DIV
                width: "100\%"
                # User Settings Bar
                DIV
                    width: "min(400px, 75%)"
                    marginLeft: "auto"
                    marginRight: "auto"
                    display: "flex"
                    flexDirection: "row"
                    alignItems: "center"
                    DIV
                        width: "2em"
                        height: "2em"
                        borderRadius: "2em"
                        backgroundColor: "#aaa"
                    H3
                        marginLeft: "5px"
                        c.user.name
                    DIV
                        flexGrow: 1
                    BUTTON
                        height: "2em"
                        marginRight: "5px"
                        onClick: (e) ->
                            s.show = !s.show ? true
                            save s
                        "Settings"

                    BUTTON
                        height: "2em"
                        onClick: (e) ->
                            c.logout = true
                            save c
                        "Log Out"
                # Slidegram and web-of-trust, or account settings
                if s.show then SETTINGS() else NETWORK()
                # Submit Box
                DIV
                    width: "min(600px, 90%)"
                    marginLeft: "auto"
                    marginRight: "auto"
                    display: "flex"
                    flexDirection: "row"
                    justifyContent: "center"
                    DIV
                        fontWeight: "bold"
                        marginRight: "15px"
                        "New Post:"
                    INPUT
                        id: "post-title"
                        placeholder: "Title"
                        flexGrow: "3"
                    INPUT
                        id: "post-url"
                        placeholder: "URL"
                        flexGrow: "2"
                        marginLeft: "5px"
                        marginRight: "15px"
                    BUTTON
                        onClick: (e) ->
                            title = document.getElementById("post-title")
                            link = document.getElementById("post-url")
                            make_post title.value, link.value, c.user.key
                            title.value = ""
                            link.value = ""
                        "Submit"
        else
            LOGIN
                c: c

    dom.NETWORK = ->
        # // My IDE doesn't know that this isn't javascript
        # // Two things go here:
        # // The users slidergram (allowing you to change the vote you've previously made)
        # // A box to create a new vote
        DIV
            width: "min(700px, 95%)"
            marginLeft: "auto"
            marginRight: "auto"
            padding: "0 15px 15px 15px"
            display: "flex"
            alignItems: "center"
            flexDirection: "column"

            MULTIGRAM
                sldr: 'multigram'
                width: 500
                height: 40
                max_avatar_radius: 15
            # Does this thing need to have suggestions?
            DIV {},
                INPUT
                    placeholder: "user/someone"
                    id: "add-follow"
                BUTTON
                    marginLeft: "5px"
                    onClick: (e) ->
                        t = unslash (document.getElementById("add-follow").value)
                        c = fetch "/current_user"
                        unless (t.startsWith "user\/" ) and ((fetch "/#{t}").name?) and (c.logged_in)
                            console.log JSON.stringify fetch "/#{t}"
                            return
                        # Ok, the input is the username of a real user, and we are also logged in.
                        # But have we already voted on them?
                        key = "/votes/_#{unslash c.user.key}_#{t}_"
                        if (fetch key).name?
                            # Also clear the input
                            document.getElementById("add-follow").value = ""
                            return
                        # Ok, we have not voted on this user yet.
                        # Make a default vote.
                       
                        userkey = c.user.key
                        votes = fetch "/votes_on/#{t}"
                        votes.values ?= []
                        votes.values.push
                            user: userkey
                            value: 0.5
                            key: key
                            target: "/#{t}"
                        save votes

                        # clear input
                        document.getElementById("add-follow").value = ""
                    "Add"

    multigram_aux = {}

    bus('multigram').to_fetch = () ->
        c = fetch "/current_user"
        unless c.logged_in
            return {key: 'muligram'}

        votes_arr = Object.values(fetch "/votes_by#{c.user.key}")
            .filter (v) ->
                unless v.target?
                    return false
                (unslash v.target).startsWith "user"
            .map (v) ->
                # Subscribe to each individual vote...
                fetch v
                # Now make a copy of it that isnt tied to the real state url
                t = Object.assign {}, v
                delete t.key
                t

        # if we want to "suggest" users in the extended network that could be voted on, we should pull them from "weights" and include them here.

        Object.assign multigram_aux, {values: votes_arr}

    bus('multigram').to_save = (val, t) ->

        #// Save the auxilliary data
        Object.assign multigram_aux, val
        #// We don't actually want to save thing like mouse state to the server, but we need to keep a copy of it.

        #// The multigram can't add new values, so we just have to save the values in it to the server.
        #// Ie, the multigram has an array of values, each of which is an actual vote state.
        #// But, we only save these if the user is done dragging!
        unless val.dragging
            val.values.forEach (v) ->
                cop = Object.assign {}, v
                cop.key = "/votes/_#{unslash v.user}_#{unslash v.target}_"
                save cop
        #// Then save it:
        t.done val


</script>
<script src="https://stateb.us/client6.js" server="http://localhost:3006"></script>
<script src="/coffee/post.coffee" crossorigin=anonymous></script>
<script src="/coffee/login.coffee" crossorigin=anonymous></script>

<!-- For slidergrams -->

<script src="https://considerit.us:3006/client/shared.coffee"></script>
<script src="/coffee/avatar.coffee" crossorigin=anonymous></script>
<script src="/coffee/slidergrams.coffee" crossorigin=anonymous></script>
<script src="/coffee/multigrams.coffee" crossorigin=anonymous></script>

<script src="https://considerit.us:3006/static/vendor/md5.js"></script>
<script src="https://considerit.us:3006/static/vendor/d3.quadtree.js"></script>
