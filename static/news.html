<!DOCTYPE html>
<html>
<head>
<meta charset=UTF-8>
<title>Braid News</title>
<style>
    body {
        margin: min(4vw, 25px);
        font-family: Lato, sans-serif;
    }
    .material-icons { user-select: none; }
    /* Rules for using icons as black on a light background. */
    .material-icons.md-dark { color: rgba(0, 0, 0, 0.54); }
    .material-icons.md-dark.md-inactive { color: rgba(0, 0, 0, 0.26); }

    /* Rules for using icons as white on a dark background. */
    .material-icons.md-light { color: rgba(255, 255, 255, 1); }
    .material-icons.md-light.md-inactive { color: rgba(255, 255, 255, 0.3); }

    .post-title:link {
        color: black;
    }
    .post-title:visited {
        color: #777;
    }
    .post-title:hover {
        color: #111;
    }

</style>
<script type=coffee>
    body_width = 750
    margin_left = 32
    post_width = 525

    document.title = 'Braid News'
    window.unslash = (t) -> if t?.startsWith?("/") then t.substr(1) else t
    window.slash = (t) -> if t?.startsWith?("/") then t else "/#{t}"

    dom.BODY = ->
        DIV
            key: "body"
            width: body_width
            margin: "auto"

            H1
                key: "page-title"
                marginLeft: margin_left
                fontSize: "50px"
                "Braid"
            
            USER()
            BIG_MULTIGRAM()
            SUBMIT_POST()
            POSTS()

    dom.POSTS = ->

        # Local copy of posts, ordered.
        posts = (fetch "/posts").all ? []

        ordered = sort_posts posts

        DIV 
            key: "post-list"
            for post in ordered
                RENDER_POST
                    post: post

    # The layout for a single post, including slidergram and such
    dom.RENDER_POST = ->
        post = @props.post
        # Subscribe to the post
        if post.key then fetch post
        unless post.user?
            # The post has actually just been deleted.
            return

        author = fetch post.user

        c = fetch '/current_user'

        # Compute the pretty-url
        prefixes = ["https://", "http://", "braid:"]
        url = if post.url.startsWith "javascript:" then "" else post.url

        pretty_url = url
        functional_url = url
        if url.startsWith "https://"
            pretty_url = url.substr(8)
            functional_url = url
        else if url.startsWith "http://"
            pretty_url = url.substr(7)
            functional_url = "https://#{pretty_url}"
        else if not url.startsWith "braid:" and url.length
            functional_url = "https://#{url}"

        user_clickable = c.logged_in and \
            (c.user.key != author.key) and \
            not (fetch "/votes/_#{unslash c.user.key}_#{unslash author.key}_")?.value?
        user_clickable = false


        DIV
            marginTop: "10px"
            marginBottom: "10px"
            display: "grid"
            grid: "\"icon title slider del\" auto
                   \". domain slider . \" 16px
                    / #{margin_left}px #{post_width + 10}px 1fr #{margin_left}px"
            alignItems: "center"

            AVATAR
                key: "avatar"
                user: author
                gridArea: "icon"
                style:
                    width: margin_left - 10
                    height: margin_left - 10
                    borderRadius: "50%"
                    alignSelf: "start"
                    cursor: if user_clickable then "pointer"
                onClick: if user_clickable then (e) ->
                    console.log "Clicking on a user does nothing, pending a $1M UX focus group."

            A
                key: "title"
                className: "post-title"
                gridArea: "title"
                fontSize: "18px"
                paddingRight: "10px"
                lineHeight: "#{margin_left - 10}px"
                justifySelf: "stretch"
                textDecoration: "none"
                href: if post.url.length then functional_url
                "#{post.title}"

            SPAN
                key: "url"
                gridArea: "domain"
                fontSize: "12px"
                color: "#999"
                whiteSpace: "nowrap"
                overflowX: "hidden"
                textOverflow: "ellipsis"
                "#{pretty_url}"

            DIV
                key: "post-votes-slider"
                gridArea: "slider"
                alignSelf: "start"
                height: margin_left - 10
                SLIDERGRAM
                    sldr: "/votes_on#{post.key}"
                    width: body_width - 2*margin_left - post_width - 10
                    height: margin_left - 10
                    max_avatar_radius: (margin_left - 10) / 2
                    read_only: !c.logged_in
                    vote_key: "user"

            if c.logged_in and c.user.key == author.key then SPAN
                key: "delete-btn"
                color: "#999"
                className: "material-icons md-dark"
                fontSize: "#{margin_left - 10}px"
                cursor: "pointer"
                textAlign: "end"
                onClick: (e) ->
                    delete_post(post)
                "delete"


    # Information about the current user:
    # Login/Out/Register
    dom.USER = ->
        c = fetch '/current_user'
        s = fetch "show_settings"
        s.show ?= false

        DIV
            position: "absolute"
            top: 15
            right: 30
            zIndex: 5

            DIV 
                key: "user-header"
                if c.logged_in
                    DIV
                        display: "flex"
                        flexDirection: "row"
                        alignItems: "center"
                        justifyContent: "end"
                        marginBottom: "5px"

                        AVATAR
                            key: "user-avatar"
                            user: c.user
                            hide_tooltip: true
                            style:
                                placeSelf: "center"
                                width: 20
                                height: 20
                                borderRadius: "50%"

                        DIV
                            key: "user-name"
                            marginLeft: "6px"
                            marginRight: "6px"
                            c.user.name

                        SPAN
                            key: "user-settings-btn"
                            className: "material-icons md-dark"
                            fontSize: "20px"
                            cursor: "pointer"
                            marginRight: "5px"
                            onClick: (e) ->
                                s.show = !s.show ? true
                                save s
                            "settings"
                        SPAN
                            key: "user-logout-btn"
                            className: "material-icons md-dark"
                            fontSize: "20px"
                            cursor: "pointer"
                            onClick: (e) ->
                                c.logout = true
                                save c
                            "logout"
                else if !s.show
                    DIV
                        onClick: (e) ->
                            s.show = !s.show ? true
                            save s
                        fontSize: "14px"
                        color: "#666"
                        cursor: "pointer"
                        "Not Logged In"
            DIV
                key: "user-modal"
                display: unless s.show then "none"
                if c.logged_in
                    SETTINGS()
                else
                    LOGIN()


    dom.SUBMIT_POST = ->

        @local.typed ?= false

        c = fetch "/current_user"
        unless c.logged_in
            return

        form_submit = =>
            title = document.getElementById("post-title")
            link = document.getElementById("post-url")
            if title.value.length > 1
                make_post title.value, link.value, c.user.key
                title.value = ""
                link.value = ""
                @local.typed = false
                save @local

        DIV
            key: "submit-container"
            width: post_width
            marginLeft: margin_left
            INPUT
                key: "submit-title"
                id: "post-title"
                style:
                    width: post_width
                    height: "28px"
                placeholder: "Say something..."
                onInput: (e) =>
                    @local.typed = e.target.value.length > 0
                    save @local

            DIV
                key: "submit-modal"
                display: if @local.typed then "flex" else "none"
                flexDirection: "row"
                marginTop: "5px"
                INPUT
                    key: "submit-url"
                    id: "post-url"
                    type: "url"
                    placeholder: "URL"
                    flexGrow: "1"
                    marginRight: "5px"
                    style:
                        height: "28px"
                    onKeyDown: (e) =>
                        if e.keyCode == 13
                            form_submit()

                SPAN
                    key: "submit-btn"
                    alignSelf: "center"
                    className: "material-icons md-dark"
                    fontSize: "24px"
                    onClick: form_submit
                    cursor: "pointer"
                    "post_add"

    dom.BIG_MULTIGRAM = ->
        DIV
            key: "multigram-wrapper"
            width: body_width
            padding: "0 #{margin_left}px 15px #{margin_left}px"

            MULTIGRAM
                sldr: 'multigram'
                width: body_width - 2 * margin_left 
                height: 130
                max_avatar_radius: 60

    multigram_aux = {}

    bus('multigram').to_fetch = () ->
        c = fetch "/current_user"
        unless c.logged_in
            # We clear the multigram_aux whenever the user is logged out.
            # Theres also a to_save handler on current_user that will clear it if the login state or our userkey changes.
            multigram_aux = {}
            return {key: 'multigram'}

        # Use this to put (potentially stale?) sizes on the votes
        aux_sizes = {}
        (multigram_aux.values ? []).forEach (v) ->
            aux_sizes[v.target] = v.size

        users = {}
        # Add users that weve voted on
        Object.values(fetch "/votes_by#{c.user.key}")
            .filter (v) ->
                unless v.target?
                    return false
                (unslash v.target).startsWith "user"
            .forEach (v) ->
                # Subscribe to each individual vote...
                fetch v
                # Now make a copy of it that isnt tied to the real state url
                t = Object.assign {}, v
                delete t.key
                t.size = aux_sizes[slash t.target]
                users[slash t.target] = t

        # Users in our network
        Object.entries(fetch "/weights#{c.user.key}")
            .filter ([k, v]) -> k != "key"
            .forEach ([k, v]) ->
                kk = slash k
                users[kk] ?= {
                    user: c.user.key
                    target: kk
                    type: "network"
                    # weights get computed between -1 and 1.
                    # but the vote value should be between 0 and 1.
                    value: (v + 1) / 2
                    size: aux_sizes[kk]
                }

        # Users that we havent voted on
        (fetch "/all_users").all
            .forEach (v) ->
                vv = slash v
                users[vv] ?= {
                    user: c.user.key
                    target: vv
                    type: "remote"
                    value: 0.5
                    size: aux_sizes[vv]
                }

        # We have to make sure not to put ourselves in the array.
        if users.hasOwnProperty c.user.key
            delete users[c.user.key]

        votes_arr = Object.values users

        Object.assign multigram_aux, {values: votes_arr}

    bus('multigram').to_save = (val, t) ->

        #// Once done dragging, save whichever one was changed.
        unless val.dragging
            val.values.forEach (v) ->
                if v.target == val.target
                    if v.type?
                        #// This is a NEW vote that didn't exist before.
                        delete v.type

                    cop = Object.assign {}, v
                    cop.key = "/votes/_#{unslash v.user}_#{unslash v.target}_"
                    #// Only store computed positions locally
                    delete cop.size
                    save cop
                       
        #// We don't actually want to save thing like mouse state to the server, but we need to keep a copy of it.
        #// Save the auxilliary data
        Object.assign multigram_aux, val
        #// Then save it:
        t.done val

    bus('/current_user').to_save = (val, old, t) ->
        if (old.logged_in != val.logged_in) or (old.user?.key != val.user?.key)
            multigram_aux = {}
        t.done val


</script>
<script src="/client.js" server="/"></script>
<script src="/coffee/post.coffee" crossorigin=anonymous></script>
<script src="/coffee/login.coffee" crossorigin=anonymous></script>

<!-- For slidergrams -->
<script src="/static/vendor/considerit_shared.js" crossorigin=anonymous></script>
<script src="/coffee/avatar.coffee" crossorigin=anonymous></script>
<script src="/coffee/slidergrams.coffee" crossorigin=anonymous></script>
<script src="/coffee/multigrams.coffee" crossorigin=anonymous></script>

<script src="https://unpkg.com/blueimp-md5@2.19.0/js/md5.js"></script>
<script src="/static/vendor/quadtree.js"></script>

<!-- Google Fonts: Lato -->
<link rel="preconnect" href="https://fonts.googleapis.com">
<link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
<link href="https://fonts.googleapis.com/css2?family=Lato:wght@300;400;700&display=swap" rel="stylesheet">
<link href="https://fonts.googleapis.com/icon?family=Material+Icons" rel="stylesheet">

<!-- Favicon -->
<link rel="icon" type="image/svg+xml" href="/static/favicon.svg" sizes="any">
</head>
</html>
