<!DOCTYPE html>
<html>
<head>
<meta charset=UTF-8>
<title>PeeryView 2.0</title>
<style>
    body {
        margin: min(4vw, 25px);
        font-family: Lato, sans-serif;
    }
    .material-icons { user-select: none; }
    /* Rules for using icons as black on a light background. */
    .material-icons.md-dark { color: rgba(0, 0, 0, 0.54); }
    .material-icons.md-dark.md-inactive { color: rgba(0, 0, 0, 0.26); }

    /* Rules for using icons as white on a dark background. */
    .material-icons.md-light { color: rgba(255, 255, 255, 1); }
    .material-icons.md-light.md-inactive { color: rgba(255, 255, 255, 0.3); }

    .post-title:link {
        color: black;
    }
    .post-title:visited {
        color: #777;
    }
    .post-title:hover {
        color: #111;
    }
    .tooltip {
        width: -moz-fit-content;
        width: -webkit-fit-content;
        width: fit-content;
    }
    .hover-select:hover {
        background-color: #eee;
    }

</style>

<script type=coffee>
    body_width = 800
    margin_left = 40
    post_width = 575

    window.unslash = (t) -> if t?.startsWith?("/") then t.substr(1) else t
    window.slash = (t) -> if t?.startsWith?("/") then t else "/#{t}"
    window.titlecase = (t) -> t.split(" ").map( (w) => w[0].toUpperCase() + w.substr 1).join(" ")
    
    window.change_path = (path) ->
        u = new URL window.location.href
        u.pathname = path
        history.pushState {}, '', u

    window.parse_path = (path) ->
        path = slash unescape path
        if path.endsWith "/"
            path = path[...-1]
        v = fetch "view"
        v.tag = v.user_key = v.page = null
        switch
            when path == "/users"
                v.page = "users"
            when path.startsWith "/user/"
                v.user_key = path
                v.page = "posts"
            when path.length > 2
                v.tag = unslash path
                v.page = "posts"
            else
                v.page = "posts"
        save v

    window.load_path = (path) ->
        change_path path
        parse_path path


    parse_path ((new URL window.location.href).pathname)

    dom.BODY = ->
        v = fetch "view"
        DIV
            width: body_width
            margin: "auto"
            
            HEADER
                key: "header"
            BIG_MULTIGRAM
                key: "multigram"

            if v?.page == "users"
                USERS
                    key: "users"
            else
                POSTS
                    key: "posts"
            

    dom.BIG_MULTIGRAM = ->
        c = fetch "/current_user"
        v = fetch "view"

        username = v.user_key ? c?.user?.key ? "/user/default" 
        params = 
            computed: true
            tag: v.tag
            untagged: !v.tag
        w = body_width - 2 * margin_left

        DIV
            width: w
            padding: "0 #{margin_left}px 0 #{margin_left}px"

            MULTIGRAM
                key: "multigram-inner"
                sldr: "#{username}/votes/people#{stringify_kson params}"
                width: w
                height: 130
                max_avatar_radius: 60
                read_only: (v.user_key) or !c.logged_in
                onsave: (vote) ->
                    # Wait, why do we need to do this on a copy?
                    copy = Object.assign {}, vote
                    copy.key = "#{username}/vote/#{unslash vote.target_key}"
                    copy.depth = 1
                    if v.tag?
                        copy.key = "#{copy.key}(tag:#{v.tag})"
                        copy.tag = v.tag
                    save copy

    r = () ->
        parser = PPPParser bus
        parser("weights/user/<userid>").to_fetch = (key, t) ->
            {userid} = t._path
            {tag, untagged} = t._params
            blob = stringify_kson {computed: true, tag: tag, untagged}

            votes = fetch "/user/#{userid}/votes/people#{blob}"
            weights_ret = {key: key}
            (votes.arr ? []).forEach (v) ->
                fetch v
                weights_ret[v.target_key] = 2 * v.value - 1
            weights_ret

        parser("score/post/<postid>").to_fetch = (key, t) ->
            {postid} = t._path
            {user, tag} = t._params

            user ?= "/user/default"

            weights = fetch "weights/#{unslash user}#{stringify_kson {tag}}"
                
            now = Date.now() / 1000

            kson = stringify_kson
                tag: tag
                untagged: !tag
        
            #// Subscribe to the post
            p = fetch "/post/#{postid}"

            sum_votes = 0
            sum_weights = 0

            #// Subscribe to the post's votes
            (fetch "/votes/#{unslash p.key}#{kson}")?.arr?.forEach (v) ->
                # first subscribe to the vote
                if v.key then fetch v
                voter = v.user_key
                # Keep track of the total weight of votes, and of the weighted sum of votes.
                sum_weights += Math.abs(weights[voter] ? 0)
                sum_votes   += (2 * v.value - 1) * (weights[voter] ? 0)

            #// Our network-weight on the author
            author_weight = weights[p.user_key] ? 0

            res = compute_score
                age: now - p.time
                author: author_weight
                score: sum_votes
                volume: sum_weights

            {
                key: key,
                value: res
            }


    if document.readyState == "complete"
        r()
    else
        window.addEventListener("DOMContentLoaded", () => r())


</script>

<!-- Main statebus script -->
<script src="/client.js" server="/"></script>
<script src="/static/vendor/considerit_shared.js" crossorigin=anonymous></script>
<script src="/coffee/parser.coffee" crossorigin=anonymous></script>
<script src="/coffee/ui.coffee" crossorigin=anonymous></script>
<script src="/coffee/post.coffee" crossorigin=anonymous></script>

<!-- For slidergrams -->
<script src="/coffee/avatar.coffee" crossorigin=anonymous></script>
<script src="/coffee/slidergrams.coffee" crossorigin=anonymous></script>
<script src="/coffee/multigrams.coffee" crossorigin=anonymous></script>

<script src="https://unpkg.com/blueimp-md5@2.19.0/js/md5.js"></script>
<script src="/static/vendor/quadtree.js"></script>

<!-- Google Fonts: Lato -->
<link rel="preconnect" href="https://fonts.googleapis.com">
<link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
<link href="https://fonts.googleapis.com/css2?family=Lato:wght@300;400;700&display=swap" rel="stylesheet">
<link href="https://fonts.googleapis.com/icon?family=Material+Icons" rel="stylesheet">

<!-- Favicon -->
<link rel="icon" type="image/svg+xml" href="/static/favicon.svg" sizes="any">
</head>
</html>
